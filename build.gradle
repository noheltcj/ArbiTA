def sharedGroupId = 'com.arbita'
def sharedVersion = '0.0.1'

group sharedGroupId
version sharedVersion

buildscript {
    ext {
        /** maven */
        shared_group_id = 'com.arbita'
        shared_version = '0.0.1'

        /** build configuration */
        kotlin_version = '1.2.51'
        gradle_version = '3.1.3'
        jvm_target = '1.8'

        /** test dependencies */
        junit_version = '4.12'
        hamcrest_version = '1.3'
        spectrum_version = '1.2.0'
        kluent_version = '1.28'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects {
    apply {
        plugin 'kotlin'
        plugin 'maven-publish'
    }

    repositories {
        jcenter()
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            localSnapshot(MavenPublication) {
                groupId = shared_group_id
                version = shared_version
                artifactId = "arbita-$project.name"

                from components.java

                artifact sourcesJar
            }
        }

        repositories {
            mavenLocal()
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    }
}
